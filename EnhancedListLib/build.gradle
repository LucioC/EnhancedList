apply plugin: 'android-library'

repositories {
    mavenCentral()
}

dependencies {
    compile 'com.android.support:support-v4:19.0.+'
    compile 'com.android.support:appcompat-v7:20.+'
    compile 'com.android.support:recyclerview-v7:21.0.0'
    compile 'com.nineoldandroids:library:2.4.0'
}

android {
    compileSdkVersion 19
    buildToolsVersion "19.1"

    defaultConfig {
        minSdkVersion 8
        targetSdkVersion 19
        versionName "0.0.1"
        versionCode 1
    }

    lintOptions {
        abortOnError false
    }
}

apply plugin: 'maven'
apply plugin: 'signing'

version = android.defaultConfig.versionName
group = "com.luciocossio.android"
archivesBaseName = "EnhancedList"

signing {
    required { has("release") && gradle.taskGraph.hasTask("uploadArchives") }
    sign configurations.archives
}

uploadArchives {

    configuration = configurations.archives
    repositories.mavenDeployer {

        beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

        repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maen2/") {
            authentication(userName: ossrhUsername, password: ossrhPassword)
        }

        snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
            authentication(userName: ossrhUsername, password: ossrhPassword)
        }

        pom.project {

            name 'EnhancedList'
            packaging 'aar'
            description 'List with enhanced features for Android'
            url 'https://github.com/LucioC/EnhancedList.git'

            scm {
                url 'scm:git@github.com:LucioC/EnhancedList.git'
                connection 'scm:git@github.com:LucioC/EnhancedList.git'
                developerConnection 'scm:git@github.com:LucioC/EnhancedList.git'
            }

            licenses {
                license {
                    name 'The Apache Software License, Version 2.0'
                    url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    distribution 'repo'
                }
            }

            developers {
                developer {
                    id 'lucioc'
                    name 'Lucio Cossio'
                    email 'lucio.cossio@gmail.com'
                }
            }

        }

    }

}
